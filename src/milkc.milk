process = require('process')
fs = require('fs')
path = require('path')
minimist = require('minimist')
helper = require('./helper')
milk = require('./milk')

argv = minimist(process.argv[2...])

endsWithMilk = filename => path.extname(filename) == '.milk'
replaceMilk = filename => filename[0 ... filename.length - 6] + '.js'

function readdirRecursive(p):
    items = fs.readdirSync(p).map(x => path.join(p, x))

    for i, item in items if fs.statSync(item).isDirectory():
        items = items.concat(readdirRecursive(item))

    return items.filter(x => fs.statSync(x).isFile())

// Main

if argv._.length > 0:
    [p, ...] = argv._
    [stats, deststats] = [fs.statSync(p), argv.o ? fs.statSync(argv.o) : null]
    files = []

    if stats.isFile():
        files = [p]
    else if stats.isDirectory():
        files = readdirRecursive(p).filter(endsWithMilk)
    else:
        console.error('Invalid input.')
        return

    if deststats != null && !deststats.isDirectory():
        console.error('Invalid output.')
        return

    for i, file in files:
        data = fs.readFileSync(file, 'utf-8')
        output = null
        console.error('Compiling ' + path.basename(file) + '...')

        try:
            output = milk.compile(data)
        catch {message, offset}:
            [row, col] = helper.offsetToLinePos(offset, data)
            console.error(message + ' @' + row + ':' + col)
            return

        if deststats == null:
            console.log(output)
        else:
            dir = p
            if stats.isFile(): dir = path.dirname(p)
            milkfile = (replaceMilk @ path.join)(argv.o, path.relative(dir, file))

            fs.writeFileSync(milkfile, output)

        percent = Math.round((i + 1) * 100 / files.length)
        console.error(percent + '%')
